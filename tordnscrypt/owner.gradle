plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def keystorePropertiesFile = file("/home/alexander/.local/KStore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31


    flavorDimensions "version", "processor"

    signingConfigs {
        stablesign {
            storeFile file("/home/alexander/.local/KStore/pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keySAlias']
            keyPassword keystoreProperties['keySPassword']
        }

        betasign {
            storeFile file("/home/alexander/.local/KStore/pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyBAlias']
            keyPassword keystoreProperties['keyBPassword']
        }
    }

    productFlavors {
        lite {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "5.5.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        pro {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "5.5.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        beta {
            applicationId "pan.alexander.tordnscrypt"
            versionName "1.4.6"
            dimension = 'version'
            signingConfig signingConfigs.betasign
            resValue 'string', 'package_name', applicationId
        }

        google_play {
            minSdkVersion 22
            applicationId "pan.alexander.tordnscrypt.gp"
            versionName "5.5.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
            resValue 'string', 'gp_property', keystoreProperties['gpPublicKey']
        }

        armv7a {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64 {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 146

        resConfigs "en", "ru", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt-rBR"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            multiDexEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.betasign
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '30.0.3'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions{
        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
    }

    applicationVariants.all { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion * 1000 + defaultConfig.versionCode
        }
    }

    ndkVersion '23.0.7421159'

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

}



repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //implementation 'androidx.appcompat:appcompat:1.4.0-rc01' Android Emoji Policy February 2
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.4.0'
    google_playImplementation 'com.android.billingclient:billing:4.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    //noinspection GradleDynamicVersion
    implementation 'eu.chainfire:libsuperuser:1.1.0.+'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation project(':filepicker')

    //DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Multidex
    debugImplementation "androidx.multidex:multidex:$multidex_version"
}
