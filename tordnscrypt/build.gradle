plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id "kotlin-parcelize"
}

android {
    compileSdk 35

    namespace 'pan.alexander.tordnscrypt'

    flavorDimensions = ["version", "processor"]

    productFlavors {

        fdroid {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "7.1.0"
            dimension = 'version'
            resValue 'string', 'package_name', applicationId
        }

        armv7a {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64 {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

        universal {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'universal'
            versionCode = 5

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 250

        resConfigs "en", "ru", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt", "pt-rBR", "el", "tr", "it", "uk", "bg", "ar", "nl", "ta"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DANDROID_ARM_NEON=ON"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def buildDate = System.getenv("SOURCE_DATE_EPOCH") == null
                ? System.currentTimeMillis() : 1000 * Long.parseLong(System.getenv("SOURCE_DATE_EPOCH"))
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + buildDate + "L)"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    buildToolsVersion = '36.0.0'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            version "3.31.6"
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions {
        jniLibs.useLegacyPackaging = true

        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
        doNotStrip '*/*/libconjure.so'
        doNotStrip '*/*/libwebtunnel.so'
    }

    applicationVariants.configureEach { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion + defaultConfig.versionCode * 100
        }
    }

    ndkVersion '23.1.7779620'

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    ksp {
        arg("dagger.fastInit", "enabled")
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'eu.chainfire:libsuperuser:1.1.1'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':filepicker')

    //DI
    implementation "com.google.dagger:dagger:$dagger_version"
    ksp "com.google.dagger:dagger-compiler:$dagger_version"
    //Persistent Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
}
