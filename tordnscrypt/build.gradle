plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk 34

    namespace 'pan.alexander.tordnscrypt'

    flavorDimensions = ["version", "processor"]

    productFlavors {

        fdroid{
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "6.8.0"
            dimension = 'version'
            resValue 'string', 'package_name', applicationId
        }

        armv7a{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

        universal{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'universal'
            versionCode = 4

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 232

        resConfigs "en", "ru", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt", "pt-rBR", "el", "tr", "it", "uk", "bg", "ar"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DANDROID_ARM_NEON=ON"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    buildToolsVersion = '34.0.0'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions{
        jniLibs.useLegacyPackaging = true

        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
        doNotStrip '*/*/libconjure.so'
        doNotStrip '*/*/libwebtunnel.so'
    }

    applicationVariants.configureEach { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion + defaultConfig.versionCode  * 100
        }
    }

    ndkVersion '23.0.7421159'

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kapt {
        javacOptions {
            option("-Adagger.fastInit=enabled")
        }
    }

}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.lifecycle:lifecycle-process:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'eu.chainfire:libsuperuser:1.1.1'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':filepicker')

    //DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    //Persistent Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
}
